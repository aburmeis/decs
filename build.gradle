plugins {
    id 'com.jfrog.bintray' version '1.8.5'
    id 'nebula.optional-base' version '5.0.3'
    id 'java'
    id 'com.github.spotbugs' version '4.6.0'
    id 'jacoco'
    id 'maven-publish'
}

ext {
    spotbugsVersion = '4.1.1'
    junitVersion = '5.7.0'
    branch = getBranch()
    versionSuffix = getVersionSuffix(branch)
    if (!project.hasProperty('bintrayUser')) {
        bintrayUser = System.getProperty("user.name")
    }
    if (!project.hasProperty('bintrayKey')) {
        bintrayKey = ''
    }
}

sourceCompatibility = 1.8
version = "${majorVersion}.${minorVersion}.${patchVersion}${versionSuffix}"

repositories {
    jcenter()
}

dependencies {
    compileOnly "com.github.spotbugs:spotbugs-annotations:${spotbugsVersion}"

    implementation "com.sun.activation:javax.activation:1.2.0", optional

    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}",
        "org.junit.jupiter:junit-jupiter-params:${junitVersion}",
        'org.hamcrest:hamcrest-library:1.3',
        'org.mockito:mockito-core:3.0.0'

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

test {
    useJUnitPlatform {
        includeEngines 'junit-jupiter'
        excludeTags 'performance'
    }
    testLogging {
        events 'skipped', 'failed'
    }
    reports {
        html.enabled = true
    }
}

task performance(type: Test) {
    useJUnitPlatform {
        includeEngines 'junit-jupiter'
        includeTags 'performance'
    }
    testLogging {
        events 'skipped', 'failed'
    }
    reports {
        html.enabled = true
    }
}

javadoc {
    options.memberLevel = 'PROTECTED'
    options.author = false
    options.header = project.name
    options.links(["http://docs.oracle.com/javase/8/docs/api/"] as String[])
    options.encoding = 'UTF-8'
    options.addStringOption('Xdoclint:none', '-quiet')
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

tasks.withType(Jar) {
    manifest {
        attributes(
            "Automatic-Module-Name": "diergo.decs",
            "Specification-Title": project.description,
            "Specification-Version": "${majorVersion}.${minorVersion}",
            "Specification-Vendor": vendor,
            "Implementation-Title": project.name,
            "Implementation-Version": "${majorVersion}.${minorVersion}.${patchVersion}",
            "Implementation-Vendor": vendor,
            "Implementation-Vendor-Id": project.group
        )
    }
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

jacoco {
    toolVersion = '0.8.6'
}

spotbugs {
    toolVersion = spotbugsVersion
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    archiveClassifier = "javadoc"
    from javadoc
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'CLASS'
                minimum = 0.9
            }
            limit {
                counter = 'LINE'
                minimum = 0.9
            }
            limit {
                counter = 'BRANCH'
                minimum = 0.8
            }
        }
    }
}

task downloadTestData {
    ext.localCopy = file('src/test/resources/worldcities.csv')
    ext.tempFile = file('build/resources/worldcities.zip')
    outputs.file localCopy
    doLast {
        println "Loading test data from https://simplemaps.com/data/world-cities"
        ant {
            get(
                    src: "https://simplemaps.com/static/data/world-cities/basic/simplemaps_worldcities_basicv1.73.zip",
                    dest: tempFile,
            )
        }
        copy {
            from(zipTree(tempFile)) {
                include '*.csv'
            }
            into localCopy.parentFile
        }
    }
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayKey
    pkg {
        repo = 'maven'
        name = project.name
        desc = project.description
        version {
            name = project.version
            vcsTag = "v${project.version}"
        }
    }
    publications = ['mavenJava']
}

check.dependsOn jacocoTestCoverageVerification, jacocoTestReport
build.dependsOn javadoc
performance.dependsOn downloadTestData

wrapper {
    gradleVersion = '6.7.1'
}

def getBranch() {
    return "git rev-parse --abbrev-ref HEAD".execute().text.trim()
}

def getVersionSuffix(branch) {
    switch (branch) {
        case ~/(master|version.*)/:
            return ''
        default:
            return '-SNAPSHOT'
    }
}

